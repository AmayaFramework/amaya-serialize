plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    compileJava.options.encoding('UTF-8')
    withJavadocJar()
    withSourcesJar()
}

group = 'io.github.amayaframework'
archivesBaseName = 'serialize'
version = System.getenv('RELEASE_VERSION') ?: '1.0.0'

repositories {
    mavenCentral()
    maven {
        url "https://s01.oss.sonatype.org/content/repositories/iogithubamayaframework-1139"
    }
    maven {
        url "https://s01.oss.sonatype.org/content/repositories/iogithubamayaframework-1140"
    }
}

dependencies {
    implementation group: 'org.atteo.classindex', name: 'classindex', version: '3.4'
    annotationProcessor group: 'org.atteo.classindex', name: 'classindex', version: '3.4'
    implementation group: 'io.github.amayaframework', name: 'core-api', version: '5'
    implementation group: 'io.github.amayaframework', name: 'filters', version: '1.0.8'
    implementation group: 'io.github.amayaframework', name: 'core-sun', version: '5'
    // TODO Remove
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
}

jar {
    from sourceSets.main.output
    from sourceSets.main.allJava
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // Specify artifacts
            artifactId = archivesBaseName
            groupId = group
            version = version
            from components.java
            // Configure pom
            pom {
                name.set('serialize')
                description.set('A plugin that allows you to easily integrate support for the required serializer.')
                url.set('https://github.com/AmayaFramework/amaya-serialize')
                organization {
                    name.set('io.github.amayaframework')
                    url.set('https://github.com/AmayaFramework')
                }
                issueManagement {
                    system.set('GitHub')
                    url.set('https://github.com/AmayaFramework/amaya-serialize/issues')
                }
                licenses {
                    license {
                        name.set('GNU General Public License v3.0')
                        url.set('https://github.com/AmayaFramework/amaya-serialize/blob/main/LICENSE')
                    }
                }
                scm {
                    url.set('https://github.com/AmayaFramework/amaya-serialize')
                    connection.set('scm:https://github.com/AmayaFramework/amaya-serialize.git')
                    developerConnection.set('scm:https://github.com/AmayaFramework/amaya-serialize.git')
                }
                developers {
                    developer {
                        id.set('RomanQed')
                        name.set('Roman Bakaldin')
                        email.set('gbakaldin@gmail.com')
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username System.getenv('SONATYPE_USERNAME')
                password System.getenv('SONATYPE_PASSWORD')
            }
        }
    }
}